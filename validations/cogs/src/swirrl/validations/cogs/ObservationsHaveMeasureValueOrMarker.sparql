PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX sdmx-attribute: <http://purl.org/linked-data/sdmx/2009/attribute#> 

#! COGS Data - Measure dimension consistent
# Base RDF Data Cube IC-15
# In a qb:DataSet which uses a Measure dimension then each qb:Observation must have a value for the measure corresponding to its given qb:measureType. 
# In PMD data, measure values can have an option obsStatus marker to note a reason for a missing measure value 
SELECT ?obs
WHERE {
    # BIND(<http://gss-data.org.uk/graph/gss_data/trade/hmrc-alcohol-bulletin> AS ?g ) 
    GRAPH ?g {
            # Observation in a measureType-cube
        ?obs qb:dataSet/qb:structure ?dsd .
        # That uses the measureType dimension 
        ?dsd qb:component/qb:dimension qb:measureType .
        # Observation measure
        ?obs qb:measureType ?measure .
        
        # Exclude any observations that are using an #obsStatus marker to note some reason for a missing measure value 
        MINUS {
            # COGS specific markers that allow an observation without a value to pass validation
            VALUES ?marker {
                # <http://gss-data.org.uk/def/concept/marker/>
                <http://gss-data.org.uk/def/concept/marker/not-applicable>
                <http://gss-data.org.uk/def/concept/marker/suppressed>
                <http://gss-data.org.uk/def/concept/marker/unknown>
                <http://gss-data.org.uk/def/concept/marker/disclosive>
                <http://gss-data.org.uk/def/concept/marker/itis-nil>
                <http://gss-data.org.uk/def/concept/marker/nil-or-less-than-a-million>
                <http://gss-data.org.uk/def/concept/marker/not-available>
                <http://gss-data.org.uk/def/concept/marker/nil-or-less-than-half-the-final-digit-shown>
                # <http://gss-data.org.uk/def/concept/marker/estimated>
                # <http://gss-data.org.uk/def/concept/marker/provisional>
            }
            ?obs sdmx-attribute:obsStatus ?marker 
        }
        
        # Must have value for its measureType
        FILTER NOT EXISTS { ?obs ?measure [] }
    }
} 
LIMIT 10