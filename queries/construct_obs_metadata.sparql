
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcat:    <http://www.w3.org/ns/dcat#>
PREFIX pmdds:   <http://publishmydata.com/def/dataset#>   
PREFIX pmdcat:  <http://publishmydata.com/pmdcat#>
PREFIX gdp:     <http://gss-data.org.uk/def/gdp#>
PREFIX void:    <http://rdfs.org/ns/void#>

CONSTRUCT {
    GRAPH ?new_graph {

         # The PMD Catalog entry's contents is the RDF data cube (and is given a type to display it as such)   
        ?dataset a pmdcat:DataCube . 

        # The PMD catalog entry
        ?entry a pmdcat:Dataset ;
            pmdcat:datasetContents ?dataset ;
            pmdcat:graph ?data_graph ;
            rdfs:label ?lbl ;
            rdfs:comment ?comment ;
            dcat:contactPoint ?contactPoint ;
            dcat:keyword ?keyword ;
            dcat:landingPage ?landingPage ;
            dcat:theme ?theme ;
            dcat:distribution ?dist ;
            dcterms:modified ?modified ;
            dcterms:issued ?issued ;
            dcterms:creator ?creator ;
            dcterms:publisher ?publisher ;
            dcterms:title ?title ;
            dcterms:references ?refs ;
            dcterms:license ?license ;
            dcterms:contributor ?contributor ;
            dcterms:description ?description ;
            gdp:family ?family ;
            void:sparqlEndpoint ?endpoint .
        
        # The PMD catalog record
        ?record 
                a dcat:CatalogRecord ;
                foaf:primaryTopic ?entry ;
                pmdcat:metadataGraph ?new_graph ;
                dcterms:issued ?issued ;
                dcterms:modified ?modified ;
                rdfs:label ?record_lbl .
        
        # The PMD catalog 
        ?catalog dcat:record ?record .
        
    }
}
{
    # BIND(<http://gss-data.org.uk/graph/gss_data/trade/ons-fdi> AS ?graph)
    BIND(<http://gss-data.org.uk/catalog/datasets> AS ?catalog)
    BIND( IRI (CONCAT(STR(?graph), "/metadata")) AS ?old_graph)
    BIND( IRI (CONCAT(STR(?graph), "-metadata")) AS ?new_graph)
    GRAPH ?old_graph {
        ?dataset a pmdds:Dataset .
        
        {
            ?dataset rdfs:label ?lbl ;
                dcterms:title ?title ;
                dcterms:creator ?creator ;
                pmdds:graph ?data_graph .
        }
        UNION {
            ?dataset dcterms:modified ?modified . 
        }
        UNION {
            ?dataset dcterms:publisher ?publisher .  
        }
        UNION {
            ?dataset dcterms:description ?description .  
        }
        UNION {
            ?dataset dcterms:contributor ?contributor .  
        }
        UNION {
            ?dataset dcterms:issued ?issued .   
        }
        UNION {
            ?dataset dcterms:license ?license .
        }
        UNION {
            ?dataset dcterms:references ?refs .
        }
        UNION {
            ?dataset rdfs:comment ?comment .
        }
        UNION {
            ?dataset gdp:family ?family .
        }
        UNION {
            ?dataset void:sparqlEndpoint ?endpoint .
        }
        UNION {
            ?dataset dcat:keyword ?keyword .
        }
        UNION {
            ?dataset dcat:landingPage ?landingPage .
        }
        UNION {
            ?dataset dcat:theme ?theme .
        }
        UNION {
            ?dataset dcat:distribution ?dist .
        }

        UNION {
            ?dataset pmdds:contactEmail ?pmdContactEmail .
        }
        UNION {
            ?dataset dcat:contactPoint ?dcatContactPoint .
        }
        BIND ( IF (BOUND (?dcatContactPoint), ?dcatContactPoint, ?pmdContactEmail )  as ?contactPoint  ) .


        BIND( IRI (CONCAT(STR(?dataset), "-catalog-record")) AS ?record)
        BIND( IRI (CONCAT(STR(?dataset), "-catalog-entry")) AS ?entry)
        BIND( CONCAT(?lbl, " Catalog Record") AS ?record_lbl)
    }
}